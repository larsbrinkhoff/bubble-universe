	TITLE BUBBLE UNIVERSE

;Source:
;https://oldbytes.space/@zxdunny/109342959566427298
;https://twitter.com/yuruyurau/status/1226846058728177665

A=1
B=A+1
C=3
I=4
J=5
U=6
V=7
X=10
RI=11
T=12
DOT=13
P=17

DIS==130

PDLLEN=20
TABSIZ=8192.

N=200.
IJSTEP=1304.	;TABSIZ/2PI
RISTEP=35.	;2PI/235*IJSTEP
TSTEP=33.	;0.025*IJSTEP

LOC 41
	JRST NTS

LOC 100

GO:	SETO A,
	.IOTLSR A,
NTS:	CONO DIS,100
	CONSO DIS,200
	 JRST .-1
	DATAO DIS,[020016]
	MOVE DOT,[220000,,022000]

	MOVE P,[-PDLLEN,,PDL]
	PUSHJ P,MAKTAB

	MOVEI U,0
	MOVEI V,0
	MOVEI X,0
	MOVEI T,0

TLOOP:	MOVEI I,0
	MOVEI RI,0
ILOOP:	MOVEI J,0
JLOOP:	MOVE A,I
	ADD A,V			;i+v
	ANDI A,TABSIZ-1
	MOVE U,SINTAB(A)	;u = sin(i+v)
	ADDI A,TABSIZ/4
	MOVE V,SINTAB(A)	;v = cos(i+v)
	MOVE A,RI
	ADD A,X			;r*i+x
	ANDI A,TABSIZ-1
	ADD U,SINTAB(A)		;u += sin(r*i+x)
	ADDI A,TABSIZ/4
	ADD V,SINTAB(A)		;v += cos(r*i+x)
	MOVE X,U
	ADD X,T			;x = u+t

	MOVE A,U
	IDIVI A,6		;Divide by 6 to scale to +-512.
	ADDI A,512.
	DPB A,[001200,,DOT]
	MOVE A,V
	IDIVI A,6
	ADDI A,512.
	DPB A,[221200,,DOT]
	CONSO DIS,200
	 JRST .-1
	DATAO DIS,DOT		;Draw a point.

	ADDI J,IJSTEP
	CAMGE J,[N*IJSTEP]
	 JRST JLOOP
	ADDI I,IJSTEP
	ADDI RI,RISTEP
	CAMGE I,[N*IJSTEP]
	 JRST ILOOP
	ADDI T,TSTEP
	JRST TLOOP	

MAKTAB: MOVE C,[-TABSIZ-TABSIZ/4,,SINTAB]
	MOVE A,[0.0]
MAKT0:	PUSH P,A
	PUSHJ P,SIN
	FMPR A,[1303.7972938088067]	;TABSIZ/2PI
	MULI A,400
	TSC A,A
	ASH B,-243(A)
	MOVEM B,(C)
	POP P,A
	FADR A,[0.0007669903939428206]	;2PI/TABSIZ
	AOBJN C,MAKT0
	POPJ P,

PDL:	BLOCK PDLLEN
SINTAB:	BLOCK TABSIZ+TABSIZ/4

;This is copied from Spacewar.
SIN:	CAMG A,SC9
	CAMGE A,[-.000211431983]
	JRST .+2
	POPJ P,
	FDVR A,SC1
	PUSH P,A
	PUSH P,B
	MULI A,400
	TSC A,A
	ASH B,-243(A)
	MOVNS A,B
	ANDCMI A,1
	TLC A,232000
	FAD A,A
	FADRB A,-1(P)
	TRNE B,2
	MOVNS A,-1(P)
	FMP A,A
	MOVE B,SC9
	FMP B,A
	FAD B,SC7
	FMP B,A
	FAD B,SC5
	FMP B,A
	FAD B,SC3
	FMP A,B
	FADR A,SC1
	FMPRM A,-1(P)
	POP P,B
	POP P,A
	POPJ P,

SC1:	1.5707963267
SC3:	-0.64596371106
SC5:	0.07968967928
SC7:	-0.00467376557
SC9:	0.00015148419

END GO
